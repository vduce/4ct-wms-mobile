import java.io.FileInputStream
import java.util.Properties

plugins {
    id("com.android.application")
    // The Flutter Gradle Plugin must be applied after the Android and Kotlin Gradle plugins.
    id("dev.flutter.flutter-gradle-plugin")
}

val keystorePropertiesFile = rootProject.file("key.properties")
val keystoreProperties = Properties()
if (keystorePropertiesFile.exists()) {
    FileInputStream(keystorePropertiesFile).use { keystoreProperties.load(it) }
}

android {
    namespace = "com.fourct.washroomops"
    compileSdk = 37
    ndkVersion = flutter.ndkVersion

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }

    defaultConfig {
        applicationId = "com.fourct.washroomops"
        minSdk = flutter.minSdkVersion
        targetSdk = flutter.targetSdkVersion
        versionCode = flutter.versionCode
        versionName = flutter.versionName
    }

    flavorDimensions += "environment"
    productFlavors {
        create("dev") {
            dimension = "environment"
            applicationIdSuffix = ".dev"
            resValue("string", "app_name", "4CT Washroom Ops Dev")
        }
        create("qa") {
            dimension = "environment"
            applicationIdSuffix = ".qa"
            resValue("string", "app_name", "4CT Washroom Ops QA")
        }
        create("prod") {
            dimension = "environment"
            resValue("string", "app_name", "4CT Washroom Ops")
        }
    }

    buildFeatures {
        resValues = true
    }

    signingConfigs {
        create("release") {
            keystoreProperties.getProperty("keyAlias")?.let { keyAlias = it }
            keystoreProperties.getProperty("keyPassword")?.let { keyPassword = it }
            keystoreProperties.getProperty("storePassword")?.let { storePassword = it }
            keystoreProperties.getProperty("storeFile")?.let { storeFile = file(it) }
        }
    }

    buildTypes {
        release {
            signingConfig = signingConfigs.getByName("release")
        }
    }
}

kotlin {
    compilerOptions {
        jvmTarget = org.jetbrains.kotlin.gradle.dsl.JvmTarget.JVM_17
    }
}

flutter {
    source = "../.."
}
